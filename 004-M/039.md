Curly Infrared Peacock

medium

# Missing checks for staking limit

## Summary
In the `RENZO_RESTAKE_MANAGER.depositETH()` function, there is a requirement that `totalTVL` must not exceed `maxDepositTVL`. However, there is no corresponding check in the `RenzoAdapter._stake() `function, which may lead to stake failures.

## Vulnerability Detail
In the `RsETHAdapter._stake()` and `PufETHAdapter._stake()` functions, as shown in the following code snippet, the protocol checks the current staking limit before staking to prevent DoS attacks. 
```solidity
     // Check LRTDepositPool stake limit
        uint256 stakeLimit = RSETH_DEPOSIT_POOL.getAssetCurrentLimit(Constants.ETH);
        if (stakeAmount > stakeLimit) {
            // Cap stake amount
            stakeAmount = stakeLimit;
        }

```



However, in the `RenzoAdapter._stake()` function, there is no corresponding check. 

```solidity
    /// @notice Mint .
    /// @dev Need to check the current staking limit before staking to prevent DoS.
    function _stake(uint256 stakeAmount) internal override returns (uint256) {
        if (stakeAmount == 0) return 0;
        if (RENZO_RESTAKE_MANAGER.paused()) revert ProtocolPaused();
        uint256 balanceBefore = EZETH.balanceOf(address(this));
        IWETH9(Constants.WETH).withdraw(stakeAmount);
        RENZO_RESTAKE_MANAGER.depositETH{value: stakeAmount}(0);
        uint256 newBalance = EZETH.balanceOf(address(this));
        if (newBalance - balanceBefore == 0) revert InvariantViolation();

        return stakeAmount;
    }


```

In fact, in the `RENZO_RESTAKE_MANAGER.depositETH()` function, the protocol checks that the `totalTVL` is less than `maxDepositTVL`. If it exceeds `maxDepositTVL`, the function will revert. 
https://etherscan.deth.net/address/0x74a09653A083691711cF8215a6ab074BB4e99ef5
```solidity
    function depositETH(uint256 _referralId) public payable nonReentrant notPaused {
        // Get the total TVL
        (, , uint256 totalTVL) = calculateTVLs();

        // Enforce TVL limit if set
        if (maxDepositTVL != 0 && totalTVL + msg.value > maxDepositTVL) {
            revert MaxTVLReached();
        }

        // Deposit the ETH into the DepositQueue
        depositQueue.depositETHFromProtocol{ value: msg.value }();

        // Calculate how much ezETH to mint
        uint256 ezETHToMint = renzoOracle.calculateMintAmount(
            totalTVL,
            msg.value,
            ezETH.totalSupply()
        );

        // Mint the ezETH
        ezETH.mint(msg.sender, ezETHToMint);

        // Emit the deposit event
        emit Deposit(msg.sender, IERC20(address(0x0)), msg.value, ezETHToMint, _referralId);
    }

```


Therefore, the protocol lacks this check here, which fails to prevent DoS.

## Impact
There is no check for the staking limit, causing users unable to stake.



## Code Snippet

https://github.com/sherlock-audit/2024-05-napier-update/blob/main/napier-uups-adapters/src/adapters/renzo/RenzoAdapter.sol#L60-L70

## Tool used

Manual Review

## Recommendation
The suggested fix is to check the staking limit.








